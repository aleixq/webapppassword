# Use `just <recipe>` to run a recipe
# https://just.systems/man/en/

# By default, run the `--list` command
default:
    @just --list

# Variables
# Try to use "docker compose" and fall back to "docker-compose" if not available

dockerComposeCommand := `docker compose > /dev/null && echo docker compose || echo docker-compose`
containerName := "nextcloud-webapppassword-app-1"
volumeName := "nextcloud-webapppassword_nextcloud"

# Aliases

alias remove-volumes := reset

# Build the docker image
build:
    {{ dockerComposeCommand }} build

# Fetch the database from the container
fetch-db:
    {{ dockerComposeCommand }} run --rm app su -c "cp data/mydb.db apps/webapppassword" www-data

# Push the database to the container
push-db:
    {{ dockerComposeCommand }} run --rm app su -c "cp apps/webapppassword/mydb.db* data" www-data

# Open a shell in the container
bash:
    {{ dockerComposeCommand }} run --rm app su -c "bash" www-data

# Open a shell in the container as root
bash-root:
    {{ dockerComposeCommand }} run --rm app bash

# Run a sidecar container with more debugging tools
slim-shell:
    slim debug --target {{ containerName }}

# Turn off maintenance mode
maintenance-mode-off:
    {{ dockerComposeCommand }} run --rm app su -c "./occ maintenance:mode --off" www-data

# Run the app:check-code command
check-code:
    {{ dockerComposeCommand }} run --rm app su -c "./occ app:check-code webapppassword" www-data

# Run the app signing command for Nextcloud
sign-app:
    {{ dockerComposeCommand }} run --rm app ../sign-app.sh www-data

# Run the app signing command for Owncloud
sign-app-owncloud:
    {{ dockerComposeCommand }} run --rm app ../sign-app-owncloud.sh

# Show Nextcloud logs
show-log:
    {{ dockerComposeCommand }} run --rm app tail -f /var/www/html/data/nextcloud.log

run-unit-test:
  {{ dockerComposeCommand }} run --rm app su -c "cd apps/webapppassword && php ./vendor/phpunit/phpunit/phpunit -c phpunit.xml" www-data

run-integration-test:
  {{ dockerComposeCommand }} run --rm app su -c "cd apps/webapppassword && php ./vendor/phpunit/phpunit/phpunit -c phpunit.integration.xml" www-data

# Remove the docker volume
reset:
    docker compose down
    docker volume rm {{ volumeName }}

# Open the browser with the app
[group('dev')]
open-browser:
    xdg-open http://localhost:8081
